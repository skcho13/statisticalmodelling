TITLE: Homework Achievement Minority measured
DATA: 
    FILE IS hw latent matrix.txt;
    TYPE IS MEANS STDEVIATIONS CORRELATION;
    NOBSERVATIONS ARE 1000;


VARIABLE:    NAMES ARE  
    Minority
    BYPARED	
    BYFAMINC	
    PAROCC	
    BYTXRSTD	
    BYTXMSTD	
    BYTXSSTD	
    BYTXHSTD	
    HW_8	
    HW10	
    ENG_12	
    MATH_12	
    SCI_12	
    SS_12;

USEVARIABLES ARE 
    Minority
    BYPARED	
    BYFAMINC	
    PAROCC	
    BYTXRSTD	
    BYTXMSTD	
    BYTXSSTD	
    BYTXHSTD	
    HW_8	
    HW10	
    ENG_12	
    MATH_12	
    SCI_12	
    SS_12;


MODEL:        
   

    FAMBACK BY 
        PAROCC
        BYPARED	
        BYFAMINC;	

    PREVACH BY
        BYTXRSTD	
        BYTXMSTD	
        BYTXSSTD	
        BYTXHSTD;	

    HW BY
        HW10
        HW_8;	

    GRADES BY
        ENG_12	
        MATH_12	
        SCI_12	
        SS_12;

    BYTXRSTD	WITH ENG_12;
    BYTXMSTD	WITH MATH_12;	
    BYTXSSTD	WITH SCI_12;
    BYTXHSTD	WITH SS_12;	

    PREVACH ON FAMBACK Minority;
    GRADES ON PREVACH HW;
    HW ON PREVACH FAMBACK Minority;

    Minority WITH FAMBACK;

MODEL INDIRECT :

    GRADES IND PREVACH FAMBACK;
    GRADES IND HW PREVACH;
    GRADES IND HW FAMBACK;
    HW IND PREVACH FAMBACK;


OUTPUT:  SAMPSTAT STDYX RESIDUAL TECH1 TECH8; 

