TITLE: Homework Latent Model 1
DATA: 
    FILE IS hw latent matrix.txt;
    TYPE IS MEANS STDEVIATIONS CORRELATION;
    NOBSERVATIONS ARE 1000;


VARIABLE:    NAMES ARE  
    Minority bypared	byfaminc	parocc	bytxrstd
    bytxmstd	bytxsstd	bytxhstd	hw_8	hw10
    eng_12	math_12	sci_12	ss_12;


USEVARIABLES ARE 
    MINORITY
    BYPARED	
    BYFAMINC	
    PAROCC	
    BYTXRSTD	
    BYTXMSTD	
    BYTXSSTD	
    BYTXHSTD	
    HW_8	
    HW10	
    ENG_12	
    MATH_12	
    SCI_12	
    SS_12;


MODEL:        
    EMinor BY Minority;
    Minority@.0099;

    FAMBACK BY 
        PAROCC
        BYPARED	
        BYFAMINC;	

    PREVACH BY
        BYTXRSTD	
        BYTXMSTD	
        BYTXSSTD	
        BYTXHSTD;	

    HW BY
        HW10
        HW_8;	

    GRADES BY
        ENG_12	
        MATH_12	
        SCI_12	
        SS_12;

    BYTXRSTD	WITH ENG_12;
    BYTXMSTD	WITH MATH_12;	
    BYTXSSTD	WITH SCI_12;
    BYTXHSTD	WITH SS_12;	

    PREVACH ON FAMBACK EMinor;
    GRADES ON PREVACH HW;
    HW ON PREVACH FAMBACK EMinor;

MODEL INDIRECT :

    GRADES IND PREVACH FAMBACK;
    GRADES IND PREVACH EMinor;
    GRADES IND HW FAMBACK;
    GRADES IND HW EMinor;
    HW IND PREVACH FAMBACK;
    HW IND PREVACH EMinor;


OUTPUT:  SAMPSTAT STDYX RESIDUAL; 

